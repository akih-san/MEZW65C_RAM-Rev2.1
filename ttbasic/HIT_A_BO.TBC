1 REM =================================================
2 REM      HIT AND BLOW BY KOJI IIGURA, 2022.
3 REM      MODIFIED BY HTTPS://GITHUB.COM/AKIH-SAN 2024
4 REM =================================================
5 PRINT "=== HIT AND BLOW ==="
6 INPUT "INPUT RANDOM SEED: " S
7 GOSUB 100; rem generate random number (u,v,w)
10 GOSUB 400;GOSUB 500;PRINT "H=",h," B=", L
20 IF H=3 GOTO 1000
30 GOTO 10
90 REM-----------------------------------
95  REM RANDOM, ANSWER IS U,V,W
100 X=S
110 REM RETRY
120 X=rnd(S)
130 IF X<0 X=-X
140 Z=X
150 GOSUB 200 ;REM DECOMPOSE INTO INDIVIDUAL DIGITS
160 GOSUB 300 ;REM CHECK CONSTRAINT 
170 IF R=1 GOTO 190
180 GOTO 110 ;REM RETRY!
190 REM FOUND NUMBER
191 PRINT ">>> RANDOM VALUE IS READY."; U=A;V=B;W=C
192 REM PRINT "U=",U," V=",V," W=",W
195 RETURN
199 REM -----------------------------------
200 REM DECOMPOSE: Z -> A,B,C
210 Q=Z/100; A=Q-Q/10*10
220 Q=Z/10; B=Q-Q/10*10
230 C=Z-Z/10*10
240 RETURN
299 REM -----------------------------------
300 REM CHECK CONSTRAINT: A,B,C -> R
301 REM IF OK, R=1 OTHERWISE R=0
310 R=-1
320 IF A=B R=0 RETURN
330 IF B=C R=0 RETURN
340 IF C=A R=0 RETURN
350 R=1
360 RETURN
399 REM -----------------------------------
400 REM GET USER INPUT -> A,B,C
410 REM RETRY
420 INPUT "GUESS THE NUMBER(GIVE UP = -1): " Y
430 IF Y<0 GOTO 2000; REM GIVE UP
440 Z=Y; GOSUB 200; GOSUB 300
450 IF R=1 RETURN
460 PRINT "INVALID NUMBER. GUESS AGAIN."
470 GOTO 410; REM RETRY
499 REM -----------------------------------
500 REM CALC H AND B
510 H=0; REM RESET HIT
520 IF A=U H=H+1
530 IF B=V H=H+1
540 IF C=W H=H+1
550 L=0; REM RESET BLOW
560 IF A=V L=L+1
570 IF A=W L=L+1
580 IF B=U L=L+1
590 IF B=W L=L+1
600 IF C=U L=L+1
610 IF C=V L=L+1
620 RETURN
999 REM -----------------------------------
1000 PRINT "GREAT ANSWER!"
1010 PRINT "YOU WIN!"
1020 STOP
1999 REM -----------------------------------
2000 REM GIVE UP!
2010 PRINT "--- GAME OVER ---"
2020 PRINT "THE ANSER IS ",U,V,W
2030 STOP
